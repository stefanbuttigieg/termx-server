plugins {
  id 'maven-publish'
  id("org.owasp.dependencycheck") version "8.4.2"
}

group "com.kodality.termx"

allprojects {
  apply plugin: "java-library"
  apply plugin: "groovy"
  apply plugin: "org.owasp.dependencycheck"

  version rootProject.version
  group rootProject.group

  repositories {
    mavenCentral()
    maven { url "https://kexus.kodality.com/repository/maven-public/" }
    maven {
      url = "https://kexus.kodality.com/repository/maven-private/"
      credentials {
        username = project.hasProperty('kodalityNexusUser') ? project.property('kodalityNexusUser') : System.getenv('KODALITY_NEXUS_USER')
        password = project.hasProperty('kodalityNexusPassword') ? project.property('kodalityNexusPassword') : System.getenv('KODALITY_NEXUS_PASSWORD')
      }
    }
  }

  dependencies {
    compileOnly "org.projectlombok:lombok:1.18.30"
    annotationProcessor "org.projectlombok:lombok:1.18.30"
  }

  java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
    withSourcesJar()
  }

  configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
  }

}

subprojects { Project project ->
  apply plugin: "maven-publish"

  publishing {
    publications {
      mavenJava(MavenPublication) {
        artifactId = project.name
        from components.java
      }
    }
    repositories {
      maven {
        def releasesRepoUrl = "https://kexus.kodality.com/repository/maven-releases/"
        def snapshotsRepoUrl = "https://kexus.kodality.com/repository/maven-snapshots/"
        url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        credentials {
          username = project.hasProperty('kodalityNexusUser') ? project.property('kodalityNexusUser') : System.getenv('KODALITY_NEXUS_USER')
          password = project.hasProperty('kodalityNexusPassword') ? project.property('kodalityNexusPassword') : System.getenv('KODALITY_NEXUS_PASSWORD')
        }
      }
    }
  }

}
